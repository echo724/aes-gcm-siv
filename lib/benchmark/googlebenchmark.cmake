set(TARGET_NAME googlebenchmark)

include(ExternalProject)

if(CMAKE_SYSTEM_NAME STREQUAL "Emscripten")
	set(BENCHMARK_CMAKE_COMMAND ${EMCMAKE_COMMAND} ${CMAKE_COMMAND})
else()
	set(BENCHMARK_CMAKE_COMMAND ${CMAKE_COMMAND})
endif()

set(BENCHMARK_ARGS "-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}")

if(DEFINED ANDROID_ABI)
	list(APPEND BENCHMARK_ARGS -DANDROID_ABI:STRING=${ANDROID_ABI})
	list(APPEND BENCHMARK_ARGS -DANDROID_PLATFORM:STRING=${ANDROID_PLATFORM})
	list(APPEND BENCHMARK_ARGS -DANDROID_NDK:STRING=${ANDROID_NDK})
	list(APPEND BENCHMARK_ARGS -DCMAKE_TOOLCHAIN_FILE:PATH=${CMAKE_TOOLCHAIN_FILE})
endif()

ExternalProject_Add(${TARGET_NAME}_dl
	GIT_REPOSITORY    https://github.com/google/benchmark
	GIT_CONFIG        advice.detachedHead=false
	GIT_SHALLOW       ON
	GIT_TAG           v1.6.0
	CMAKE_CACHE_ARGS  -DCMAKE_INSTALL_PREFIX:PATH=<INSTALL_DIR> -DBENCHMARK_ENABLE_TESTING:STRING=OFF
	CMAKE_ARGS        ${BENCHMARK_ARGS}
	CMAKE_COMMAND     ${BENCHMARK_CMAKE_COMMAND}
	BUILD_COMMAND     ${CMAKE_COMMAND} --build <BINARY_DIR> --config $<CONFIG>
	UPDATE_COMMAND    ""
	)
ExternalProject_Get_property(${TARGET_NAME}_dl INSTALL_DIR)

add_library(${TARGET_NAME} INTERFACE)

target_include_directories(${TARGET_NAME}
	INTERFACE ${INSTALL_DIR}/include
	)

target_link_libraries(${TARGET_NAME}
	INTERFACE -L${INSTALL_DIR}/lib
	INTERFACE benchmark Threads::Threads
	)

add_dependencies(${TARGET_NAME} ${TARGET_NAME}_dl)
